#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрочитатьИзФайла(Команда)
	
	Если Объект.СоответствиеРеквизитовФайла.Количество() > 0 Тогда
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ОчисткаТаблицыСоответсвийЗавершение", ЭтотОбъект), 
			"Таблица соответствий будет очищена, продолжить?",
			РежимДиалогаВопрос.ОКОтмена
		);
	Иначе
		ПрочитатьИзФайлаНаКлиентеПродолжение();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыборШаблонногоФайлаЗавершение(ВыбранныеФайлы, ДопПараметры) Экспорт
	
	Диалог = ДопПараметры.Диалог; 
	
	Если (ВыбранныеФайлы <> Неопределено) Тогда				
		
		АдресВХ = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(Диалог.ПолноеИмяФайла));		
		ПрочитатьИзФайлаНаСервере(АдресВХ, Диалог.ПолноеИмяФайла);
				
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОчисткаТаблицыСоответсвийЗавершение(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		Объект.СоответствиеРеквизитовФайла.Очистить();
		Модифицированность = Истина;
		ПрочитатьИзФайлаНаКлиентеПродолжение();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьИзФайлаНаКлиентеПродолжение()
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Выберите файл";
	Диалог.ПолноеИмяФайла = ""; 
	Диалог.МножественныйВыбор = Ложь;	
	Диалог.Показать(
		Новый ОписаниеОповещения(
			"ВыборШаблонногоФайлаЗавершение", 
			ЭтотОбъект,
			Новый Структура("Диалог", Диалог)
		)
	);			
	
КонецПроцедуры


&НаСервере
Процедура ПрочитатьИзФайлаНаСервере(АдресФайла, АдресНаКлиенте)
	
	ФайлДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайла);
	ИмяФайла = "";
	Расширение = "";
	Если СтрНайти(АдресНаКлиенте, ".csv") > 0 Тогда
		ИмяФайла = ПолучитьИмяВременногоФайла(".csv");
		Расширение = ".csv";
	ИначеЕсли СтрНайти(АдресНаКлиенте, ".xlsx") > 0 Тогда
		ИмяФайла = ПолучитьИмяВременногоФайла(".xlsx");
		Расширение = ".xlsx";
	Иначе
		 ВызватьИсключение "Неподдерживаемый тип файла";
	КонецЕсли;
	
	ФайлДвоичныеДанные.Записать(ИмяФайла);
	
	МассивКолонок = Новый Массив();
	
	Если Расширение = ".csv" Тогда
		ТекстовыйФайл = Новый ТекстовыйДокумент;
		ТекстовыйФайл.Прочитать(ИмяФайла); 
		
		Разделитель = ";";
			
		Если ТекстовыйФайл.КоличествоСтрок() Тогда
			Колонки = ТекстовыйФайл.ПолучитьСтроку(1);
			МассивКолонок = СтрРазделить(Колонки, Разделитель, Ложь);  	
		КонецЕсли;	
	ИначеЕсли Расширение = ".xlsx" Тогда
		ТабличныйДокумент = Новый ТабличныйДокумент();
		ТабличныйДокумент.Прочитать(ИмяФайла, СпособЧтенияЗначенийТабличногоДокумента.Значение); 
		
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда 
			ПозицияКолонок = 2;
			МассивКолонок = Новый Массив;
			
			Для НомерКолонки = 1 По ТабличныйДокумент.ШиринаТаблицы Цикл
				ЗначениеЯчейки = revagro_ЗагрузкаФайлов.ПолучитьЗначениеЯчейки(ТабличныйДокумент, ПозицияКолонок, НомерКолонки);
				Если НЕ ЗначениеЗаполнено(ЗначениеЯчейки) Тогда
					Прервать;	
				КонецЕсли; 
				МассивКолонок.Добавить(СокрЛП(СтрЗаменить(ЗначениеЯчейки, Символы.ПС, "")));
			КонецЦикла;  
		КонецЕсли;
	КонецЕсли;
	
	УдалитьИзВременногоХранилища(АдресФайла);
	УдалитьФайлы(ИмяФайла);
	 			
	Для Каждого Колонка Из МассивКолонок Цикл
		СтрСоотвествие = Объект.СоответствиеРеквизитовФайла.Добавить();
		СтрСоотвествие.ИмяРеквизитаВФайле = Колонка;	
	КонецЦикла; 			
	
КонецПроцедуры

#КонецОбласти

